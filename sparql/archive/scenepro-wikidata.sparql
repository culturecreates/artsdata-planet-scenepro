PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
select ?scenepro ?adid ?wikidata  ?wikidataLabel  ?statedLabel ?date 
where 
{
    {
        ?adid  schema:sameAs ?scenepro .
        filter(strstarts(str(?scenepro),"http://scenepro.ca#Organization"))
        filter(strstarts(str(?adid),"http://kg.artsdata.ca/resource/"))
    } UNION {
        ?scenepro  schema:sameAs  ?adid .
        filter(strstarts(str(?scenepro),"http://scenepro.ca#Organization"))
        filter(strstarts(str(?adid),"http://kg.artsdata.ca/resource/"))
        minus {
            ?adid    schema:sameAs ?scenepro
        }
    }
    #  filter(bound(?wikidata))
    OPTIONAL {
        ?scenepro schema:sameAs ?wikidata .
        filter(strstarts(str(?wikidata),"http://www.wikidata.org/entity/"))
        SERVICE <https://query.wikidata.org/sparql> {
            SELECT ?wikidata ?statedLabel ?date WHERE {
                ?wikidata wdt:P463 wd:Q87727957;
                          p:P463 ?statement .
                ?statement ps:P463  wd:Q87727957 .
                ?statement prov:wasDerivedFrom ?ref.
                ?ref pr:P248 ?stated;
                     pr:P813 ?date.
                SERVICE wikibase:label {
                    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
                }
            }
        }
    }
}