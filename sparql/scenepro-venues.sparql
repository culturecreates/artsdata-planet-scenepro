PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
SELECT ?uri (sample(?name_language) as ?scenepro_name) ?aduri  (sample(?artsdata_name_language) as ?artsdata_name) (sample(?wduri) as ?wikidata)  ?url ?resultSeverity  WHERE {
    graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-scenepro/scenepro-orgs> {
        ?uri schema:name  ?name_language ;
             a schema:Place .
        OPTIONAL {
            ?uri schema:url ?url .
            filter(!isBLANK(?url))
        }
        OPTIONAL {
            ?shacl sh:focusNode ?uri ;
                   sh:resultSeverity ?resultSeverity .
        }
    }
    OPTIONAL {
        ?uri schema:sameAs ?aduri ;
             schema:name ?artsdata_name_language .
        filter(contains(str(?aduri),"http://kg.artsdata.ca/"))
        OPTIONAL {
            ?aduri schema:sameAs ?wduri  .
            filter(contains(str(?wduri),"wikidata.org/"))
        }
    }
    OPTIONAL {
        ?aduri schema:sameAs ?uri .
        filter(contains(str(?aduri),"http://kg.artsdata.ca/"))
        OPTIONAL {
            ?wduri schema:sameAs ?aduri .
            filter(contains(str(?wduri),"wikidata.org/"))
        }
        OPTIONAL {
            ?aduri schema:sameAs ?wduri  .
            filter(contains(str(?wduri),"wikidata.org/"))
        }
    }
} GROUP BY ?uri ?aduri  ?url  ?resultSeverity