PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
SELECT ?scenepro (sample(?name_language) as ?scenepro_name) ?artsdata  (sample(?artsdata_name_language) as ?artsdata_name) ?wikidata  ?url ?resultSeverity  
WHERE {
    graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-scenepro/scenepro-orgs> {
        ?scenepro schema:name  ?name_language ;
                  a schema:Place .
        OPTIONAL {
            ?scenepro schema:url ?url .
        }
        OPTIONAL {
            ?shacl sh:focusNode ?scenepro ;
                   sh:resultSeverity ?resultSeverity .
        }
    }
    OPTIONAL {
        ?artsdata schema:sameAs ?scenepro ;
                  schema:name ?artsdata_name_language .
        filter(contains(str(?artsdata),"http://kg.artsdata.ca/"))
        OPTIONAL {
            ?artsdata schema:sameAs ?wikidata  .
            filter(contains(str(?wikidata),"wikidata.org/"))
        }
    }
} GROUP BY ?scenepro ?artsdata ?wikidata  ?url  ?resultSeverity